apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion Versions.compileSdk
    defaultConfig {
        minSdkVersion Versions.minSdkVersion
        targetSdkVersion Versions.targetSdk
        versionCode Versions.releaseVersionCode
        versionName Versions.releaseVersionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

configurations {
    ktlint
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // project dependencies
    implementation project(ProjectModules.domain)

    // external dependencies
    api ProjectDependencies.retrofit
    implementation ProjectDependencies.rxJava2Adapter
    implementation ProjectDependencies.gson
    implementation ProjectDependencies.loggingInterceptor

    implementation ProjectDependencies.kotlin

    implementation ProjectDependencies.daggerAndroid
    implementation ProjectDependencies.daggerAndroidSupport
    kapt ProjectDependencies.daggerAnnotationProcessor
    kapt ProjectDependencies.daggerCompiler

    implementation ProjectDependencies.rxJava2
    implementation ProjectDependencies.rxAndroid
    ktlint ProjectDependencies.ktlint

    // testing dependencies
    testImplementation ProjectDependencies.junit
    testImplementation ProjectDependencies.assertJ
    testImplementation ProjectDependencies.mockitoKotlin
    androidTestImplementation ProjectDependencies.androidTestRunner
    androidTestImplementation ProjectDependencies.espresso

}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "--reporter=plain",
            "--reporter=checkstyle,output=${buildDir}/reports/ktlint/checkstyle.xml",
            "${projectDir}/**/src/**/*.kt"
}
